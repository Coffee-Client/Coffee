plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	flatDir {
		dirs 'lib'
	}
	maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org/releases"
		content {
			includeGroup 'meteordevelopment'
		}
	}
	maven {
		name = 'Ladysnake Mods'
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
		content {
			includeGroup 'io.github.ladysnake'
			includeGroupByRegex 'io\\.github\\.onyxstudios.*'
		}
	}
	maven { url 'https://jitpack.io' }
	mavenCentral()
}

configurations {
	libImpl
	modImpl
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// Lombok
	compileOnly(group: "org.projectlombok", name: "lombok", version: "1.18.24")
	annotationProcessor(group: "org.projectlombok", name: "lombok", version: "1.18.24")

	// Fabric api
	Set<String> apiModules = [
			"fabric-api-base",
			"fabric-resource-loader-v0"
	]
	// Add each module we need
	apiModules.forEach {
		modImpl(fabricApi.module(it, project.fabric_api_version))
	}

	// 3rd party
	libImpl(group: "meteordevelopment", name: "discord-ipc", version: "1.1")
	libImpl(name: "NetMC-1.0.1")
	libImpl(project(":Authlib"))
	libImpl(project(":Eventlib"))
	modImpl("com.github.0x3C50:Renderer:master-SNAPSHOT")

//	modImpl(group: "io.github.ladysnake", name: "satin", version: "1.9.0") {
//		transitive = false
//	}

	modImpl(name: "baritone-unoptimized-fabric-1.9.3")

	configurations.libImpl.dependencies.each {
		implementation(it)
//		include(it)
	}

	configurations.modImpl.dependencies.each {
		modImplementation(it)
//		implementation(it)
		include(it)
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}

	from {
		configurations.libImpl.collect { it.isDirectory() ? it : zipTree(it) }
	}
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

}
